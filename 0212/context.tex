\section{ステッパの実装方法とコンテキスト}
\label{section:context}

ステッパは small step による実行と同じなので、small step のインタプリ
タを書けば実装できる。実際、Whitington \& Ridge \cite{EPTCS294.3} は small step のインタプリ
タを書くことで OCaml に対するステッパを実装している。
しかし、ステップ実行中に関数呼び出し単位でスキップする機能をつけようと思
うとインタプリタは big step で書かれていた方が都合が良い。そこで、我々
の過去の研究 \cite{FCA19} では、big step のインタプリタを元にして
ステッパを作成している。ここでも、そのアプローチをとる。

図 \ref{figure:lambda} に OCaml による型無し$\lambda$計算の定義と代入
ベースの big step インタプリタの実装を示す。
関数 \texttt{subst :\ e -> (string * v) list -> e} は代入関数であり、
\texttt{subst e [(x, v)]} は式 \texttt{e} の中の
全ての変数 \texttt{x} を値 \texttt{v} に置換した式を返す。

環境を使わず代入に基づくインタプリタを用いるのは、
各ステップでのプログラムが手元にあると簡約位置の情報等を保持しやすいた
めである。
この情報はステッパが各ステップの簡約位置を強調して表示するために利用される。
ステップ出力のたびに出力プログラム中の変数に値を代入すれば
環境を用いたインタプリタで実装することもできると思われるが、
簡約位置の情報の保持が複雑になる可能性がある。

% 環境を使わず代入に基づくインタプリタを用いるのは
% ステップ表示において環境を表示するのではなく $\beta$ 簡約をして見せるためであり、
% 環境を用いたインタプリタで実装する場合にはステップ出力のたびに
% 出力プログラム中の変数に値を代入すればよい。

\begin{figure}
\begin{spacing}{0.9}
\begin{verbatim}
(* 値 *)
type v = Var of string      (* x *)
       | Fun of string * e  (* fun x -> e *)

(* 式 *)
and e = Val of v      (* 値 *)
       | App of e * e  (* e e *)

(* インタプリタ *)
let rec eval (exp : e) : v = match exp with
  | Val (v) -> v  (* 値ならそのまま返す *)
  | App (e1, e2) ->
    let v2 = eval e2 in  (* 引数部分を実行 *)
    let v1 = eval e1 in  (* 関数部分を実行 *)
    let reduct = match v1 with
      | Fun (x, e) -> subst e [(x, v2)]  (* 代入 e[v2/x] *)
      | _ -> failwith "type error" in  (* 関数部分が関数でなければ型エラー *)
    eval reduct  (* 代入後の式を実行 *)
\end{verbatim}
\caption{型無し$\lambda$計算とそのインタプリタ}
\label{figure:lambda}
\end{spacing}
\end{figure}

図 \ref{figure:lambda}のインタプリタをステッパにするには、簡約をする際に簡約前後のプログラ
ムを出力する機能を追加すればよい。
しかしステッパが出力したいのは実行中の部分式ではなく式全体である。
コンテキストを含めた式全体を出力するためには、実行中の式の構文木の他にコン
テキストの情報が必要である。

コンテキストの情報を得るために、Clements ら\cite{clements01} は Racket
の continuation mark を使用してコンテキストフレームの情報を記録するこ
とでステッパを実装した。本研究ではそのような特殊な機能は使わずに、
インタプリタ関数に明示的にコンテキスト情報のための引数を追加する。
図\ref{figure:lambda}のインタプリタに
その変更を施すと、図\ref{figure:lambda_stepper}のようになる。ここで、
関数 \texttt{memo :\ e -> e -> c -> unit} は、簡約前の式、簡約後の式、
コンテキスト情報の3つを引数にとり、コンテキスト情報を利用して簡約前後
の式全体をそれぞれ出力するものである。

\begin{figure}
\begin{spacing}{0.9}
\begin{verbatim}
(* コンテキスト *)
type c = CId             (* [.] *)
       | CApp2 of e * c  (* [e [.]] *)
       | CApp1 of c * v  (* [[.] v] *)

(* 出力しながら再帰的に実行 *)
let rec eval (exp : e) (c : c) : v = match exp with
  | Val (v) -> v
  | App (e1, e2) ->
    let v2 = eval e2 (CApp2 (e1, c)) in  (* コンテキストを1層深くする *)
    let v1 = eval e1 (CApp1 (c, v2)) in  (* コンテキストを1層深くする *)
    let redex = App (Val v1, Val v2) in
    let reduct = match v1 with
      | Fun (x, e) -> subst e [(x, v2)]
      | _ -> failwith "type error" in
    memo redex reduct c;  (* コンテキストを利用して式全体を出力 *)
    eval reduct c

(* 実行を始める *)
let stepper (exp : e) = eval exp CId
\end{verbatim}
\caption{型無し$\lambda$計算に対するステッパ}
\label{figure:lambda_stepper}
\end{spacing}
\end{figure}

図\ref{figure:lambda_stepper}のように、コンテキストを表すデータ型を定
義して再帰呼び出し時の構造に合わせて引数として渡すようにすれば、式全体
を再構成して出力することが可能になる。ここで、コンテキストを表すデータ
型は、評価文脈そのものになっていることに気がつく。評価文脈のデータ型は、
big step のインタプリタを CPS 変換し、非関数化すると機械的に得られるこ
とが知られている \cite{AK2010, 10.1145/1411204.1411206}。これは、我々が手動で定義したコンテキストのデータは、
機械的に導出できることを示唆している。

$\lambda$計算に対するステッパであれば、手動でコンテキストの型を定義す
るのは簡単だが、言語が複雑になってくると必ずしもこれは自明ではない。実
際、以前の研究 \cite{FCA19} で try-with 構文を含む言語のステッパを実装
したときには、コンテキストを try-with 構文で区切る必要があったため、
コンテキストの構造が一次元的でなく、リストのリストになった。
algebraic effect handlers などが入った場合、どのようなコンテキストを使えば良い
のかはまた別途、考慮する必要がある。このような場合、機械的にコンテキス
トの定義を導出できることにはメリットがある。
次節以降ではそのような方針で algebraic effect handlers に対するステッパを導出
する。
