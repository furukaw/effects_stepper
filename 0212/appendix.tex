\appendix
\section{付録}

\begin{figure}
  % code/3cps/eval.ml より
\begin{verbatim}
(* CPS インタプリタを非関数化して CPS 変換した関数 *)
let rec eval (exp : e) (k : k) (k2 : k2) : a = match exp with
  | Val (v) -> apply_in k v k2
  | App (e1, e2) -> eval e2 (FApp2 (e1, k)) k2
  | Op (name, e) -> eval e (FOp (name, k)) k2
  | With (h, e) ->
    eval e FId (fun a -> apply_handler k h a k2)  (* GHandle に変換される *)

(* handle 節内の継続を適用する関数 *)
and apply_in (k : k) (v : v) (k2 : k2) : a = match k with
  | FId -> k2 (Return v)  (* 継続を適用 *)
  | FApp2 (e1, k) -> let v2 = v in eval e1 (FApp1 (k, v2)) k2
  | FApp1 (k, v2) -> let v1 = v in
    (match v1 with
      | Fun (x, e) ->
        let reduct = subst e [(x, v2)] in
        eval reduct k k2
      | Cont (cont_value) ->
        (cont_value k) v2 k2
      | _ -> failwith "type error")
  | FOp (name, k) ->
    k2 (OpCall (name, v, (fun v -> fun k2' -> apply_in k v k2'))) (* 継続を適用 *)

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler (k : k) (h : h) (a : a) (k2 : k2) : a = match a with
  | Return v ->
    (match h with {return = (x, e)} ->
      let reduct = subst e [(x, v)] in
      eval reduct k k2)
  | OpCall (name, v, va) ->
    (match search_op name h with
      | None ->
        k2 (OpCall (name, v, (fun v -> fun k2' ->  (* 継続を適用 *)
          va v (fun a' -> apply_handler k h a' k2'))))  (* GHandle に変換 *)
      | Some (x, y, e) ->
        let cont_value =
          Cont (fun k'' -> fun v -> fun k2 ->
            va v (fun a' -> apply_handler k'' h a' k2)) in  (* GHandle に変換 *)
        let reduct = subst e [(x, v); (y, cont_value)] in
        eval reduct k k2)

(* 初期継続を渡して実行を始める *)
let interpreter (e : e) : a = eval e FId (fun a -> a)  (* GId に変換される *)
\end{verbatim}
\caption{CPS インタプリタを非関数化して CPS 変換したプログラム}
\label{figure:3cps}
\end{figure}

\begin{figure}
  % code/4defun/syntax.ml より
\begin{verbatim}
(* CPS インタプリタを非関数化して CPS 変換して非関数化した関数 *)
let rec eval (exp : e) (k : k) (k2 : k2) : a = match exp with
  | Val (v) -> apply_in k v k2
  | App (e1, e2) -> eval e2 (FApp2 (e1, k)) k2
  | Op (name, e) -> eval e (FOp (name, k)) k2
  | With (h, e) -> eval e FId (GHandle (h, k, k2))

(* handle 節内の継続を適用する関数 *)
and apply_in (k : k) (v : v) (k2 : k2) : a = match k with
  | FId -> apply_out k2 (Return v)
  | FApp2 (e1, k) -> let v2 = v in eval e1 (FApp1 (k, v2)) k2
  | FApp1 (k, v2) -> let v1 = v in (match v1 with
    | Fun (x, e) ->
      let reduct = subst e [(x, v2)] in
      eval reduct k k2
    | Cont (cont_value) ->
      (cont_value k) v2 k2
    | _ -> failwith "type error")
  | FOp (name, k) ->
    apply_out k2 (OpCall (name, v, (fun v -> fun k2' -> apply_in k v k2')))

(* 全体の継続を適用する関数 *)
and apply_out (k2 : k2) (a : a) : a = match k2 with
  | GId -> a
  | GHandle (h, k, k2) -> apply_handler k h a k2

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler (k : k) (h : h) (a : a) (k2 : k2) : a = match a with
  | Return v -> (match h with {return = (x, e)} ->
    let reduct = subst e [(x, v)] in eval reduct k k2)
  | OpCall (name, v, va) ->
    (match search_op name h with
      | None ->
        apply_out k2 (OpCall (name, v,
          (fun v -> fun k2' -> va v (GHandle (h, k, k2')))))
      | Some (x, y, e) ->
        let cont_value =
          Cont (fun k'' -> fun v -> fun k2 -> va v (GHandle (h, k'', k2))) in
        let reduct = subst e [(x, v); (y, cont_value)] in
        eval reduct k k2)

(* 初期継続を渡して実行を始める *)
let interpreter (e : e) : a = eval e FId GId
\end{verbatim}
\caption{CPS インタプリタを非関数化して CPS 変換して非関数化したプログラム}
\label{figure:4defun}
\end{figure}

\begin{figure}
  % 5memo/syntax.ml より
  % FApp1 の引数を交換、plug_all の match の中を字下げした
\begin{verbatim}
(* handle 節内の式を再構成して返す *)
let rec plug_in_handle (e : e) (k : k) : e = match k with
  | FId -> e
  | FApp2 (e1, k) -> plug_in_handle (App (e1, e)) k
  | FApp1 (k, v2) -> plug_in_handle (App (e, Val v2)) k
  | FOp (name, k) -> plug_in_handle (Op (name, e)) k

(* プログラムを再構成して返す *)
let rec plug_all (e : e) ((k, k2) : cont) : e =
  let e_in_handle = plug_in_handle e k in  (* handle 節内の式 *)
  match k2 with
    | GId -> e_in_handle  (* ハンドルが無い場合は handle 節内の式をそのまま返す *)
    | GHandle (h, k, k2) ->  (* ハンドルがある場合はさらに外に (k, k2) がある *)
      let e_handle = With (h, e_in_handle) in  (*with h handle (handle節内の式)*)
      plug_all e_handle (k, k2)
\end{verbatim}
\caption{式とコンテキスト情報を受け取ってプログラムを再構成して返す関数}
\label{figure:plug_all}
\end{figure}
