\newpage
\appendix
\section{付録}

\subsection{プログラムを再構成する関数}

\begin{figure}[h]
\begin{spacing}{0.9}
\begin{verbatim}
(* 式と handle 節内のコンテキスト情報を受け取って handle 節の式を再構成する *)
let rec plug_in_handle (e : e) (c : c) : e = match c with
  | FId -> e
  | FApp2 (e1, c) -> plug_in_handle (App (e1, e)) c
  | FApp1 (c, v2) -> plug_in_handle (App (e, Val v2)) c
  | FOp (name, c) -> plug_in_handle (Op (name, e)) c

(* 式と handle 節内と外のコンテキスト情報のペアを受け取ってプログラム全体を再構成する *)
let rec plug_all (e : e) ((c, c2) : (c * c2)) : e =
  let e_in_handle = plug_in_handle e c in
  match c2 with
  | GId -> e_in_handle
  | GHandle (h, c, c2) ->
    let e_handle = With (h, e_in_handle) in
    plug_all e_handle (c, c2)
\end{verbatim}
\end{spacing}
\caption{式とコンテキストを受け取って式全体を再構成する関数}
\label{figure:plug_all}
\end{figure}

\subsection{algebraic effect handlers のステッパによる実行の例}
\label{subsection:step_example}

\begin{figure}
\begin{spacing}{0.8}
\begin{verbatim}
Step 0:  ((with {return x -> (fun _ -> x),
                 Get(_; k) -> (fun s -> ((k s) s)),
                 Set(s; k) -> (fun _ -> ((k ()) s))}
           handle ((fun _ -> (Get ())) (Set ((Get ()) + 1)))) 0)
Step 1:  ((fun s ->
           (((fun y => (with {return x -> (fun _ -> x),
                              Get(_; k) -> (fun s -> ((k s) s)),
                              Set(s; k) -> (fun _ -> ((k ()) s))}
                        handle ((fun _ -> (Get ())) (Set (y + 1))))) s) s)) 0)
Step 2:  (((fun y => (with {return x -> (fun _ -> x),
                            Get(_; k) -> (fun s -> ((k s) s)),
                            Set(s; k) -> (fun _ -> ((k ()) s))}
                      handle ((fun _ -> (Get ())) (Set (y + 1))))) 0) 0)
Step 3:  ((with {return x -> (fun _ -> x),
                 Get(_; k) -> (fun s -> ((k s) s)),
                 Set(s; k) -> (fun _ -> ((k ()) s))}
           handle ((fun _ -> (Get ())) (Set (0 + 1)))) 0)
Step 4:  ((with {return x -> (fun _ -> x),
                 Get(_; k) -> (fun s -> ((k s) s)),
                 Set(s; k) -> (fun _ -> ((k ()) s))}
           handle ((fun _ -> (Get ())) (Set 1))) 0)
Step 5:  ((fun _ -> (((fun z => (with {return x -> (fun _ -> x),
                                       Get(_; k) -> (fun s -> ((k s) s)),
                                       Set(s; k) -> (fun _ -> ((k ()) s))}
                                 handle ((fun _ -> (Get ())) z))) ()) 1)) 0)
Step 6:  (((fun z => (with {return x -> (fun _ -> x),
                            Get(_; k) -> (fun s -> ((k s) s)),
                            Set(s; k) -> (fun _ -> ((k ()) s))}
                      handle ((fun _ -> (Get ())) z))) ()) 1)
Step 7:  ((with {return x -> (fun _ -> x),
                 Get(_; k) -> (fun s -> ((k s) s)),
                 Set(s; k) -> (fun _ -> ((k ()) s))}
           handle ((fun _ -> (Get ())) ())) 1)
Step 8:  ((with {return x -> (fun _ -> x),
                 Get(_; k) -> (fun s -> ((k s) s)),
                 Set(s; k) -> (fun _ -> ((k ()) s))} handle (Get ())) 1)
Step 9:  ((fun s -> (((fun a => (with {return x -> (fun _ -> x),
                                       Get(_; k) -> (fun s -> ((k s) s)),
                                       Set(s; k) -> (fun _ -> ((k ()) s))}
                                 handle a)) s) s)) 1)
Step 10:  (((fun a => (with {return x -> (fun _ -> x),
                             Get(_; k) -> (fun s -> ((k s) s)),
                             Set(s; k) -> (fun _ -> ((k ()) s))}
                       handle a)) 1) 1)
Step 11:  ((with {return x -> (fun _ -> x),
                  Get(_; k) -> (fun s -> ((k s) s)),
                  Set(s; k) -> (fun _ -> ((k ()) s))} handle 1) 1)
Step 12:  ((fun _ -> 1) 1)
Step 13:  1
\end{verbatim}
\end{spacing}
\caption{図 \ref{figure:6cps} のステッパを用いたステップ実行の例}
\label{figure:step_example}
\end{figure}

図\ref{figure:step_example} は、1つの値からなる状態を保持するハンドラを使ったプログラムをステップ実行した例である。

状態ハンドラは、受け取った値を記録して \texttt{()} を返す \texttt{Set} と、
\texttt{()} を受け取って記録された値を返す \texttt{Get} の 2 つのオペレーションを定義している。
\texttt{handle} 節で値が返された場合 (\texttt{return} 節) については、
状態にかかわらず値をそのまま返す。
入力プログラム (\texttt{Step 0} のプログラム) は、状態ハンドラのもとで、状態の初期値を \texttt{0} として、
\texttt{Set (Get () + 1)} をした後に \texttt{Get ()} を返すというプログラムである。

図 \ref{figure:step_example} の文字列は、
4 節で最終的に得られたステッパ (図\ref{figure:6cps}) を
整数、整数の足し算、unit 型で拡張したステッパの出力に改行とインデントを加えたものである。
プログラムが 1 段階ずつ書き換わっていて関数の内容も表示されているので、
限定継続の中身やその使われ方など理解が難しい機能の実行の様子を細かく見ることができる。
プログラムが長く読むのが大変な面はあるが、
\texttt{Step 3} でふたつ目の \texttt{Get ()} が \texttt{0} となり、
\texttt{Step 7} で \texttt{Set} により状態が \texttt{1} になり、
\texttt{Step 11} でひとつ目の \texttt{Get ()} が \texttt{1} になっているのが観察できる。

\newpage

\subsection{変換の各過程におけるインタプリタ}

\begin{figure}[!b]
\begin{spacing}{0.9}
  % code/3cps/eval.ml より
\begin{verbatim}
(* CPS インタプリタを非関数化して CPS 変換した関数 *)
let rec eval (exp : e) (k : k) (k2 : k2) : a = match exp with
  | Val (v) -> apply_in k v k2
  | App (e1, e2) -> eval e2 (FApp2 (e1, k)) k2
  | Op (name, e) -> eval e (FOp (name, k)) k2
  | With (h, e) ->
    eval e FId (fun a -> apply_handler k h a k2)  (* GHandle に変換される *)

(* handle 節内の継続を適用する関数 *)
and apply_in (k : k) (v : v) (k2 : k2) : a = match k with
  | FId -> k2 (Return v)  (* 継続を適用 *)
  | FApp2 (e1, k) -> let v2 = v in eval e1 (FApp1 (k, v2)) k2
  | FApp1 (k, v2) -> let v1 = v in
    (match v1 with
      | Fun (x, e) ->
        let reduct = subst e [(x, v2)] in
        eval reduct k k2
      | Cont (cont_value) ->
        (cont_value k) v2 k2
      | _ -> failwith "type error")
  | FOp (name, k) ->
    k2 (OpCall (name, v, (fun v -> fun k2' -> apply_in k v k2')))  (*継続を適用*)

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler (k : k) (h : h) (a : a) (k2 : k2) : a = match a with
  | Return v ->
    (match h with {return = (x, e)} ->
      let reduct = subst e [(x, v)] in
      eval reduct k k2)
  | OpCall (name, v, m) ->
    (match search_op name h with
      | None ->
        k2 (OpCall (name, v, (fun v -> fun k2' ->  (* 継続を適用 *)
          m v (fun a' -> apply_handler k h a' k2'))))  (* GHandle に変換 *)
      | Some (x, y, e) ->
        let cont_value =
          Cont (fun k'' -> fun v -> fun k2'' ->
            m v (fun a' -> apply_handler k'' h a' k2'')) in  (* GHandle に変換 *)
        let reduct = subst e [(x, v); (y, cont_value)] in
        eval reduct k k2)

(* 初期継続を渡して実行を始める *)
let interpreter (e : e) : a = eval e FId (fun a -> a)  (* GId に変換される *)
\end{verbatim}
\caption{CPS インタプリタを非関数化して CPS 変換したプログラム}
\label{figure:3cps}
\end{spacing}
\end{figure}

\begin{figure}
\begin{spacing}{0.9}
  % code/4defun/syntax.ml より
\begin{verbatim}
(* CPS インタプリタを非関数化して CPS 変換して非関数化した関数 *)
let rec eval (exp : e) (k : k) (k2 : k2) : a = match exp with
  | Val (v) -> apply_in k v k2
  | App (e1, e2) -> eval e2 (FApp2 (e1, k)) k2
  | Op (name, e) -> eval e (FOp (name, k)) k2
  | With (h, e) -> eval e FId (GHandle (h, k, k2))

(* handle 節内の継続を適用する関数 *)
and apply_in (k : k) (v : v) (k2 : k2) : a = match k with
  | FId -> apply_out k2 (Return v)
  | FApp2 (e1, k) -> let v2 = v in eval e1 (FApp1 (k, v2)) k2
  | FApp1 (k, v2) -> let v1 = v in (match v1 with
    | Fun (x, e) ->
      let reduct = subst e [(x, v2)] in
      eval reduct k k2
    | Cont (cont_value) ->
      (cont_value k) v2 k2
    | _ -> failwith "type error")
  | FOp (name, k) ->
    apply_out k2 (OpCall (name, v, (fun v -> fun k2' -> apply_in k v k2')))

(* 全体の継続を適用する関数 *)
and apply_out (k2 : k2) (a : a) : a = match k2 with
  | GId -> a
  | GHandle (h, k, k2) -> apply_handler k h a k2

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler (k : k) (h : h) (a : a) (k2 : k2) : a = match a with
  | Return v -> (match h with {return = (x, e)} ->
    let reduct = subst e [(x, v)] in eval reduct k k2)
  | OpCall (name, v, m) ->
    (match search_op name h with
      | None ->
        apply_out k2 (OpCall (name, v,
          (fun v -> fun k2' -> m v (GHandle (h, k, k2')))))
      | Some (x, y, e) ->
        let cont_value =
          Cont (fun k'' -> fun v -> fun k2 -> m v (GHandle (h, k'', k2))) in
        let reduct = subst e [(x, v); (y, cont_value)] in
        eval reduct k k2)

(* 初期継続を渡して実行を始める *)
let interpreter (e : e) : a = eval e FId GId
\end{verbatim}
\caption{CPS インタプリタを非関数化して CPS 変換して非関数化したプログラム}
\label{figure:4defun}
\end{spacing}
\end{figure}
