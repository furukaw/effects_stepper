\section{インタプリタの変換}
\label{section:transform}

本節では、\ref{section:definition}節で定義したインタプリタ
(図\ref{figure:1cps})に対して、正当性の保証された２種類のプログラム変
換（非関数化と CPS 変換）をかけることで、コンテキストを明示的に保持する
インタプリタを得て、そこからステッパを作成する方法を示す。

\subsection{非関数化}
\label{section:2defun}

インタプリタを変換してコンテキストの情報を得るには
まずCPS変換をして次に非関数化をすればよいが、
\ref{section:definition}節で示したインタプリタはオペレーション呼び出しをサ
ポートするため handle 節内の実行について最初から CPS で書かれている。
したがって、ここではまず非関数化をかける。

非関数化というのは、高階関数を１階のデータ構造で表現する方法である。高
階関数は全てその自由変数を引数に持つような１階のデータ構造となり、高階
関数を呼び出していた部分は apply 関数の呼び出しとなる。この apply 関数
は、高階関数が呼び出されていたら行ったであろう処理を行うように別途、定
義されるものである。この変換は
(非関数化する対象の関数が特定されれば)
機械的に行うことができる。

具体的に図\ref{figure:1cps}のプログラムの継続 \texttt{k} 型の$\lambda$式を
非関数化するには次のようにする。結果は図 \ref{figure:k_2defun}と図 \ref{figure:2defun}のようになる。

\begin{enumerate}
\item 継続を表す$\lambda$式をコンストラクタに置き換える。その際、$\lambda$式内の自由変数はコンストラクタの引数にする。
その結果、得られるデータ構造は図 \ref{figure:k_2defun}のようになる。
図 \ref{figure:1cps}の中には、コメントとしてどの関数がどのコンストラク
タに置き換わったのかが書かれている。
\item 関数を表すコンストラクタと引数を受け取って中身を実行するような apply 関数を定義する。
これは、図 \ref{figure:2defun}では \texttt{apply\_in} と呼ばれている。
\item $\lambda$式を呼び出す部分を、apply 関数にコンストラクタと引数を渡すように変更する。
\end{enumerate}

\begin{figure}
\begin{spacing}{0.9}
  % code/2defun/syntax.ml より
\begin{verbatim}
(* handle 内の継続 *)
type k = FId                (* [.] *)
       | FApp2 of e * k     (* [e [.]] *)
       | FApp1 of k * v     (* [[.] v] *)
       | FOp of string * k  (* [op [.]] *)

(* handle 内の実行結果 *)
type a = Return of v                            (* 前と同じ *)
       | OpCall of string * v * (v -> k2 -> a)  (* 継続をとるように変更 *)
\end{verbatim}
\caption{非関数化後の継続の型}
\label{figure:k_2defun}
\end{spacing}
\end{figure}

\begin{figure}[t]
\begin{spacing}{0.9}
  % code/2defun/eval.ml より
  % インデントした
\begin{verbatim}
(* CPS インタプリタを非関数化した関数 *)
let rec eval (exp : e) (k : k) : a = match exp with
  | Val (v) -> apply_in k v  (* 継続適用関数に継続と値を渡す *)
  | App (e1, e2) -> eval e2 (FApp2 (e1, k))
  | Op (name, e) -> eval e (FOp (name, k))
  | With (h, e) -> let a = eval e FId in  (* 空の継続を渡す *)
    apply_handler k h a  (* handle 節内の実行結果をハンドラで処理 *)

(* handle 節内の継続を適用する関数 *)
and apply_in (k : k) (v : v) : a = match k with
  | FId -> Return v  (* 空の継続、そのまま値を返す *)
  | FApp2 (e1, k) -> let v2 = v in eval e1 (FApp1 (k, v2))
  | FApp1 (k, v2) -> let v1 = v in
    (match v1 with
      | Fun (x, e) ->
        let reduct = subst e [(x, v2)] in
        eval reduct k
      | Cont (cont_value) ->
        (cont_value k) v2
      | _ -> failwith "type error")
  | FOp (name, k) ->
    OpCall (name, v, (fun v -> apply_in k v))  (* Op 呼び出しの情報を返す *)

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler (k : k) (h : h) (a : a) : a = ... (* 非関数化前(図5)と同じ *)

(* 初期継続を渡して実行を始める *)
let interpreter (e : e) : a = eval e FId
\end{verbatim}
\caption{CPS インタプリタを非関数化したプログラム}
\label{figure:2defun}
\end{spacing}
\end{figure}

非関数化した後の継続の型を見ると、ラムダ計算の通常の評価文脈に加え
てオペレーション呼び出しの引数を実行するフレーム \texttt{FOp} が加わっ
ていることがわかる。これが、handle 節内の実行のコンテキスト情報である。

ここで、\texttt{OpCall} の第３引数は非関数化されていないことに注意しよ
う。この部分は handle 節内の継続とは限らないので、ここでは非関数化せずに
もとのままとしている。

非関数化の操作自体は機械的だが、どの部分を非関数化するかを判断するには
少し意味的な判断が必要になる。基本的にはハンドラ内部の継続を表す k 型
のλ式をすべて非関数化するのだが、OpCall の第３引数は「ハンドラ内部の
継続」ではなく、オペレーション呼び出しで捕捉される継続を意味している。
この捕捉される継続はハンドラをまたがることがあり「ハンドラ内部の継続」
とは限らない。そのため fun v -> k v の形にして、k の部分のみを非関数化
し、外側の関数はそのままとした。

ここを非関数化することも可能ではあるが、そうする
と最終的に得られるコンテキスト情報がきれいなリストのリストの形にはなら
なくなってしまう。

handle 節内の評価文脈を表すデータ構造は非関数化により導くことができたが、
図 \ref{figure:2defun}のインタプリタはオペレーション呼び出しなどの実
装で継続を非末尾の位置で使っており純粋な CPS 形式にはなっていないため、
全体のコンテキストは得られていない。そのため、このコンテキストを使って
ステッパを構成してもプログラム全体を再構成することはできない。プログラ
ム全体のコンテキストを得るためには、このインタプリタに対して
もう一度 CPS 変換と非関数化を施し、純粋な CPS 形式にする必要がある。

\subsection{CPS 変換}
\label{subsection:3cps}

図\ref{figure:2defun} では、末尾再帰でない再帰呼び出しの際に継続が初期
化されてしまうせいでコンテキスト全体に対応する情報が継続に含まれていな
かった。
ここでは、全てのコンテキスト情報を明示化するため、さらに CPS 変換を施
す。
この変換によって現れる継続は \texttt{a -> a} 型である。この型
\texttt{a -> a} の名前を \texttt{k2} とする。
変換したプログラムは付録の図 \ref{figure:3cps} に示す。

このプログラムは、図 \ref{figure:2defun}のプログラムを機械的に CPS 変
換すれば得られるもので、
\texttt{OpCall} の第3引数も CPS 変換される点にさえ注意すれば、
特に説明を必要とする箇所はない。
プログラム中には、次節で非関数化する部分にその旨、コメントが付してある。
この変換により、すべての（serious な）関数呼び出しが末尾呼び出しとな
り、コンテキスト情報はふたつの継続ですべて表現される。

\subsection{非関数化}
\label{subsection:4defun}

CPS 変換ですべてのコンテキスト情報がふたつの継続に集約された。ここで
は、CPS 変換したことにより新たに現れた \texttt{a -> a} 型の関数を非関
数化してデータ構造に変換する。
非関数化によって型 \texttt{k2} の定義は図 \ref{figure:k2_4defun} に、
インタプリタは付録の図 \ref{figure:4defun} に変換される。

\begin{figure}[t]
\begin{spacing}{0.9}
  % code/4defun/syntax.ml より
\begin{verbatim}
(* 全体のメタ継続 *)
type k2 = GId
        | GHandle of h * k * k2
\end{verbatim}
\caption{2回目の非関数化後の継続の型}
\label{figure:k2_4defun}
\end{spacing}
\end{figure}

\begin{figure}
\begin{spacing}{0.9}
  % インデントした
\begin{alltt}
(* handle 節内の継続を適用する関数 *)
let rec apply_in (k : k) (v : v) (k2 : k2) : a = match k with
  | FId -> apply_out k2 (Return v)
  | FApp2 (e1, k) -> let v2 = v in eval e1 (FApp1 (k, v2)) k2
  | FApp1 (k, v2) -> let v1 = v in (match v1 with
    | Fun (x, e) ->
       \colorbox{lg}{let redex = App (Val v1, Val v2) in  (* (fun x -> e) v2 *)}
       let reduct = subst e [(x, v2)] in    \colorbox{lg}{(* e[v2/x] *)}
       \colorbox{lg}{memo redex reduct (k, k2);} eval reduct k k2
    | Cont (x, \colorbox{lg}{(k', k2'),} cont_value) ->
       \colorbox{lg}{let redex = App (Val v1, Val v2) in  (* (fun x => k2'[k'[x]]) v2 *)}
\end{alltt}
\vspace{-26pt}
\begin{alltt}
       \colorbox{lg}{let reduct = plug_all (Val v2) (k', k2') in  (* k2'[k'[v2]] *)}
\end{alltt}
\vspace{-26pt}
\begin{alltt}
       \colorbox{lg}{memo redex reduct (k, k2);} (cont_value k) v2 k2
    | _ -> failwith "type error")
  | FOp (name, k) ->
    apply_out k2 (OpCall (name, v, (k, GId),
      (fun v -> fun k2' -> apply_in k v k2')))

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler (k : k) (h : h) (a : a) (k2 : k2) : a = match a with
  | Return v ->
    (match h with \LBR{}return = (x, e)\RBR ->
      \colorbox{lg}{let redex = With (h, Val v) in  (* with \LBR{}return x -> e\RBR handle v *)}
      let reduct = subst e [(x, v)] in  \colorbox{lg}{(* e[v/x] *)}
      \colorbox{lg}{memo redex reduct (k, k2);} eval reduct k k2)
  | OpCall (name, v, \colorbox{lg}{(k', k2'),} m) -> (match search_op name h with
    | None ->
      apply_out k2 (OpCall (name, v, \colorbox{lg}{(k', compose_k2 k2' h (k, GId)),}
        (fun v -> fun k2' -> m v (GHandle (h, k, k2')))))
    | Some (x, y, e) ->
      \colorbox{lg}{(* with \LBR{}name(x; y) -> e\RBR handle k2'[k'[name v]] *)}
\end{alltt}
\vspace{-26pt}
\begin{alltt}
      \colorbox{lg}{let redex = With (h, plug_all (Op (name, Val v)) (k', k2')) in}
      let cont_value =
        Cont (\colorbox{lg}{gen_var_name (),} (k', compose_k2 k2' h (FId, GId)),
          (fun k'' -> fun v -> fun k2 -> m v (GHandle (h, k'', k2)))) in
      \colorbox{lg}{(* e[v/x, (fun n => with \LBR{}name(x; y) -> e\RBR handle k2'[k'[n]]) /y *)}
      let reduct = subst e [(x, v); (y, cont_value)] in
      \colorbox{lg}{memo redex reduct (k, k2);}
      eval reduct k k2)
\end{alltt}
\caption{変換の後、出力関数を足して得られるステッパ}
\label{figure:5memo}
\end{spacing}
\end{figure}

この非関数化によって、引数 \texttt{k} と引数 \texttt{k2} からコンテキ
スト全体の情報が得られるようになった。
ここで、得られたコンテキストの情報を整理しておこう。
\texttt{k} は handle 節内のコンテキストを示している。
\texttt{FId} 以外はいずれの構成子も \texttt{k} を引数にとっているの
で、これは \texttt{FId} を空リストととらえれば評価文脈のリストと考える
ことができる。
\texttt{k2} も同様に \texttt{h} と \texttt{k} が連なったリストと考える
ことができる。
全体として「with handle 式に囲まれた評価文脈のリスト」のリストになっており、
直感に合ったハンドラによって区切られたコンテキストが得られていることが
わかる。

得られたコンテキストはごく自然なものだが、ハンドラの入る位置などは必ず
しも自明ではない。
このコンテキストの型は
Hillerstr\"{o}m ら \cite{10.1145/2976022.2976033, e6cb0c3222794e48bf38cf44e46fe4aa} の
algebraic effect handlers の入った体系の抽象機械で継続を表すデータと
同じ構造をしている。
これは、我々の定義した big step のインタプリタをもと
に、プログラム変換をかけることでコンテキストのデータ型を導出できたこ
とを示している。

\subsection{出力}
\label{subsection:memo}

\ref{subsection:4defun} 節までの変換によって、コンテキストの情報を引数
に保持するインタプリタ関数を得ることができた。この情報を用いて簡約前後
のプログラムを出力するようにするとステッパが得られる。具体的には、簡約が起こる部分でプログラム全
体を再構成し表示するようにする。図\ref{figure:5memo}
が表示を行う関数 \texttt{memo} を
足した後の関数 \texttt{apply\_in} と \texttt{apply\_handler}
である。
(他の関数は簡約している部分が無いので図 \ref{figure:4defun} と同じになる。)
図 \ref{fugure:5memo} 中の灰色の部分が出力のために追加された部分である。

ここで関数 \texttt{memo :\ e -> e -> (k * k2) -> unit} は、簡約基とその簡約後の式と簡約時のコンテキストを受け取って、簡約前のプログラムと簡約後のプログラムをそれぞれ再構成して出力する。

図 \ref{figure:5memo}を見ると \texttt{apply\_in} では普通の関数呼び出しと継続呼び出しが
\texttt{memo} されている。また、\texttt{apply\_handler} では
handle 節の実行が正常終了した場合とオペレーション呼び出しが起きた場合に
それぞれ \texttt{memo} 関数が挿入されている。
また、オペレーション呼び出しが処理されず外側の \texttt{with handle} 文
に制御を移す際には、図 \ref{figure:compose}に示される関数を使って
コンテキストの結合を行なっている。

\begin{figure}[t]
\begin{spacing}{0.9}
  % code/5memo/eval.ml より
\begin{verbatim}
(* コンテキスト k2_in の外側にフレーム GHandle (h, k_out, k2_out) を付加する *)
let rec compose_k2 k2_in h (k_out, k2_out) =
  match k2_in with
  | GId -> GHandle (h, k_out, k2_out)
  | GHandle (h', k', k2') -> GHandle (h', k', compose_k2 k2' h (k_out, k2_out))
\end{verbatim}
\caption{継続を外側に拡張する関数}
\label{figure:compose}
\end{spacing}
\end{figure}

\begin{figure}[t]
\begin{spacing}{0.9}
  % code/6cps/syntax.ml より
\begin{verbatim}
(* 値 *)
type v = ...                                        (* 前と同じ *)
       | Cont of string * (c * c2) * ((c * k) -> k) (* 継続 *)
(* handle 内の実行結果 *)
and a = Return of v                          (* 値になった *)
      | OpCall of string * v * (c * c2) * k  (* オペレーションが呼び出された *)
(* handle 内のメタ継続 *)
and k = v -> c2 -> a
(* handle 内のコンテキスト *)
and c = FId                (* [.] *)
      | FApp2 of e * c     (* [e [.]] *)
      | FApp1 of c * v     (* [[.] v] *)
      | FOp of string * c  (* [op [.]] *)
(* 全体のコンテキスト *)
and c2 = GId
       | GHandle of h * c * c2
\end{verbatim}
\caption{継続の情報を保持するための言語やコンテキストの定義}
\label{figure:k_6cps}
\end{spacing}
\end{figure}

\begin{figure}
\begin{spacing}{0.9}
  % code/6cps/eval.ml より
  % 2マスずつインデントした
\begin{verbatim}
(* CPS ステッパ *)
let rec eval (exp : e) ((c, k) : c * k) (c2 : c2) : a = match exp with
  | Val (v) -> k v c2
  | App (e1, e2) -> eval e2 (FApp2 (e1, c), (fun v2 c2 ->
    eval e1 (FApp1 (c, v2), (fun v1 c2 -> match v1 with
      | Fun (x, e) ->
        let redex = App (Val v1, Val v2) in  (* (fun x -> e) v2 *)
        let reduct = subst e [(x, v2)] in  (* e[v2/x] *)
        memo redex reduct (c, c2); eval reduct (c, k) c2
      | Cont (x, (c', c2'), cont_value) ->
        let redex = App (Val v1, Val v2) in  (* (fun x => c2[c[x]]) v2 *)
        let reduct = plug_all (Val v2) (c', c2') in  (* c2[c[v2]] *)
        memo redex reduct (c, c2); (cont_value (c, k)) v2 c2
      | _ -> failwith "type error")) c2)) c2
  | Op (name, e) -> eval e (FOp (name, c), (fun v c2 ->
    OpCall (name, v, (c, GId), (fun v c2' -> k v c2')))) c2
  | With (h, e) ->
    let a = eval e (FId, (fun v c2 -> Return v)) (GHandle (h, c, c2)) in
    apply_handler (c, k) h a c2

(* handle 節内の実行結果をハンドラで処理する関数 *)
and apply_handler ((c, k) : c * k) (h : h) (a : a) (c2 : c2) : a = match a with
  | Return v -> (match h with {return = (x, e)} ->
    let redex = With (h, Val v) in  (* with {return x -> e} handle v *)
    let reduct = subst e [(x, v)] in  (* e[v/x] *)
    memo redex reduct (c, c2); eval reduct (c, k) c2)
  | OpCall (name, v, (c', c2'), k') ->
    (match search_op name h with
      | None -> OpCall (name, v, (c', compose_c2 c2' h (c, GId)),
        (fun v' c2'' -> let a' = k' v' (GHandle (h, c, c2'')) in
          apply_handler (c, k) h a' c2''))
      | Some (x, y, e) ->
        (* with {name(x; y) -> e} handle c2'[c'[name v]] *)
        let redex = With (h, plug_all (Op (name, Val v)) (c', c2')) in
        let cont_value = Cont (gen_var_name (),
          (c', compose_c2 c2' h (FId, GId)), (fun (c'', k'') v' c2'' ->
        let a' = k' v' (GHandle (h, c'', c2'')) in
        apply_handler (c'', k'') h a' c2)) in
        (* e[v/x, (fun n => with {name(x; y) -> e} handle c2'[c'[y]])/y *)
        let reduct = subst e [(x, v); (y, cont_value)] in
        memo redex reduct (c, c2); eval reduct (c, k) c2)

let stepper (e : e) : a = eval e (FId, (fun v c2 -> Return v)) GId
\end{verbatim}
\caption{変換の結果得られた、CPSインタプリタを基にしたステッパ}
\label{figure:6cps}
\end{spacing}
\end{figure}

\subsection{CPSインタプリタに基づいたステッパ}

前節で algebraic effect handlers を持つ言語に対するステッパを作ることができ
た。しかし、前節で作ったステッパではコンテキストの情報が非関数化されて
いた。また、CPS 変換されているためふたつの継続を扱っており、
もともとの CPS インタプリタとは形がかなり異なったものとなっている。
しかし、一度、前節までで必要なコンテキストの情報がどのようなものかが判
明すると、それを直接、もとの CPS インタプリタに加えてステッパを作るこ
とができる。

もとの CPS インタプリタの型定義に必要なコンテキストの情報を加えた定義
が図 \ref{figure:k_6cps}になる。
ここで、\texttt{c} と \texttt{c2} がそれぞれ handle 節内と全体のコンテキ
ストの情報で、前節までの非関数化によって得られたものである。
一方、\texttt{k} はもとからある高階の継続の型である。
継続 \texttt{k} は、簡約ごとにプログラム全体を表示するので、
必要なコンテキストの情報を新たに引数に取るようになっている。

このデータ定義を使って、もとの CPS インタプリタをステッパに変換したの
が図 \ref{figure:6cps}である。
図 \ref{figure:6cps} にある関数 \texttt{plug\_all :\ e -> (k * k2) -> e} は
式と「handle 節内のコンテキストとそれより外のコンテキストのペア」を受け取って
プログラム全体を再構成して返す関数であり、ステップ出力の際にも用いる
(実装は付録の図\ref{figure:plug_all} に示す)。
このインタプリタは、もとの CPS インタプリタにコンテキストの情報として
引数 \texttt{c} と \texttt{c2} を加え、簡約ごとにプログラムを再構成
し、ステップ表示するようにしたものである。
一度、必要なコンテキストの情報が特定されると、algebraic effect handlers のよう
に非自明な言語構文が入っていても、直接、ステッパを作ることができるよう
になる。
