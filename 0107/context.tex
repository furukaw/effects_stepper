%\section{ステッパの実装におけるコンテキスト}
\section{ステッパの実装方法とコンテキスト}
\label{section:context}

ステッパは small-step による実行と同じなので、small-step のインタプリ
タを書けば実装できる。実際、Whitington \& Ridge \cite{EPTCS294.3} は small-step のインタプリ
タを書くことで OCaml に対するステッパを実装している。
しかし、small-step のインタプリタをメンテナンスするのは簡単ではない。
また、ステップ実行中に関数呼び出し単位でスキップする機能をつけようと思
うとインタプリタは big-step で書かれていた方が都合が良い。そこで、我々
の過去の研究 \cite{FCA19} では、big-step のインタプリタを元にして
ステッパを作成している。ここでも、そのアプローチをとる。

図 \ref{figure:lambda} に OCaml による型無し$\lambda$計算の定義と代入
ベースの big-step インタプリタの実装を示す。
関数 \texttt{subst :\ e -> (string * v) list -> e} は代入関数であり、
\texttt{subst e [(x, v)]} は式 \texttt{e} の中の
全ての変数 \texttt{x} を値 \texttt{v} に置換した式を返す。

%ステッパはインタプリタにステップ出力機能が加わったものなので、通常のインタプリタ関数に書き加えることで実装できる。図 \ref{figure:lambda} に OCaml による型無し$\lambda$計算の定義とインタプリタの実装を示す。関数 \texttt{subst : e -> (string * v) list -> e} は代入関数であり、\texttt{subst e [(x, v)]} は式 \texttt{e} の中の全ての変数 \texttt{x} を値 \texttt{v} に置換した式を返す。

%ステッパは small-step での実行過程を見せるものであるが、small-step でなく big-step のインタプリタを基にして作ることで、関数適用や配列に対する処理やループ等のひとまとまりの実行の開始と終了を検知して「その部分の最後のステップまで飛ばす」等の機能を作ることが容易になる\ref{FCA19}ため、big-step のインタプリタを利用している。

\begin{figure}
\begin{verbatim}
(* 値 *)
type v = Var of string      (* x *)
       | Fun of string * e  (* fun x -> e *)

(* 式 *)
and e = Val of v      (* 値 *)
       | App of e * e  (* e e *)

(* インタプリタ *)
let rec eval (exp : e) : v = match exp with
  | Val (v) -> v  (* 値ならそのまま返す *)
  | App (e1, e2) ->
    let v2 = eval e2 in  (* 引数部分を実行 *)
    let v1 = eval e1 in  (* 関数部分を実行 *)
    let reduct = match v1 with
      | Fun (x, e) -> subst e [(x, v2)]  (* 代入 e[v2/x] *)
      | _ -> failwith "type error" in  (* 関数部分が関数でなければ型エラー *)
    eval reduct  (* 代入後の式を実行 *)
\end{verbatim}
\caption{型無し$\lambda$計算とそのインタプリタ}
\label{figure:lambda}
\end{figure}

このインタプリタをステッパにするには、簡約をする際に簡約前後のプログラ
ムを出力する機能を追加すればよい。
しかしステッパが出力したいのは実行中の部分式ではなく式全体であり、コン
テキストを含めた式全体を出力するためには、実行中の式の構文木の他にコン
テキストの情報が必要である。

%このインタプリタをステッパにするには、簡約をする際に簡約前後のプログラムを出力する機能を追加すればよい。
%関数 \texttt{eval} は実行する式の部分式を再帰的に実行する。すると、再帰呼び出しされた \texttt{eval} の中では、引数に実行中の部分式の情報しか与えられていない。
%しかしステッパが出力したいのは実行中の部分式ではなく式全体であり、コンテキストを含めた式全体を出力するためには、実行中の式の構文木の他にコンテキストの情報が必要である。

コンテキストの情報を得るために、Clements ら\cite{clements01} は Racket
の continuation mark を使用してコンテキストフレームの情報を記録するこ
とでステッパを実装した。本研究ではそのような特殊な機能は使わずに、
インタプリタ関数に明示的にコンテキスト情報のための引数を追加する。
図\ref{figure:lambda}のインタプリタに
その変更を施すと、図\ref{figure:lambda_stepper}のようになる。ここで、
関数 \texttt{memo :\ e -> e -> c -> unit} は、簡約前の式、簡約後の式、
コンテキスト情報の3つを引数にとり、コンテキスト情報を利用して簡約前後
の式全体をそれぞれ出力するものである。

%コンテキストの情報を得るために、Clements ら\cite{clements01} は Racket の continuation mark を使用してコンテキストフレームの情報を記録したが、本研究ではインタプリタ関数に明示的にコンテキスト情報のための引数を追加する。筆者らは以前\cite{FCA19}、実際にインタプリタに引数を追加することによってステッパを実装した。図\ref{figure:lambda}のインタプリタにその変更を施すと、図\ref{figure:lambda_stepper}のようになる。ここでは関数 \texttt{memo :\ e -> e -> c -> unit} は、簡約前の式、簡約後の式、コンテキスト情報の3つの引数をとり、コンテキスト情報を利用して簡約前後の式にそれぞれコンテキストを付加することで簡約前後の式全体を得て出力する。

\begin{figure}
\begin{verbatim}
(* コンテキスト *)
type c = CId             (* [.] *)
       | CApp2 of e * c  (* [e [.]] *)
       | CApp1 of v * c  (* [[.] v] *)

(* 出力しながら再帰的に実行 *)
let rec eval (exp : e) (c : c) : v = match exp with
  | Val (v) -> v
  | App (e1, e2) ->
    let v2 = eval e2 (CApp2 (e1, c)) in  (* コンテキストを1層深くする *)
    let v1 = eval e1 (CApp1 (v2, c)) in  (* コンテキストを1層深くする *)
    let redex = App (Val v1, Val v2) in
    let reduct = match v1 with
      | Fun (x, e) -> subst e [(x, v2)]
      | _ -> failwith "type error" in
    memo redex reduct c;  (* コンテキストを利用して式全体を出力 *)
    eval reduct c

(* 実行を始める *)
let stepper (exp : e) = eval exp CId
\end{verbatim}
\caption{型無し$\lambda$計算に対するステッパ}
\label{figure:lambda_stepper}
\end{figure}

図\ref{figure:lambda_stepper}のように、コンテキストを表すデータ型を定
義して再帰呼び出し時の構造に合わせて引数として渡すようにすれば、式全体
を再構成して出力することが可能になる。ここで、コンテキストを表すデータ
型は、評価文脈そのものになっていることに気がつく。評価文脈のデータ型は、
big-step のインタプリタを CPS 変換し、非関数化すると機械的に得られるこ
とが知られている。これは、我々が手動で定義したコンテキストのデータは、
機械的に導出できることを示唆している。

$\lambda$計算に対するステッパであれば、手動でコンテキストの型を定義す
るのは簡単だが、言語が複雑になってくると必ずしもこれは自明ではない。実
際、以前の研究 \cite{FCA19} で try-with 構文を含む言語のステッパを実装
したときには、コンテキストを try-with 構文で区切る必要があったため、
コンテキストの構造が一次元的でなく、リストのリストになった。
algebraic effects などが入った場合、どのようなコンテキストを使えば良い
のかはまた別途、考慮する必要がある。このような場合、機械的にコンテキス
トの定義を導出できることにはメリットがある。
次節以降ではそのような方針で algebraic effects に対するステッパを導出
する。

%図\ref{figure:lambda_stepper}のように、コンテキストを表すデータ型を定義して再帰呼び出し時の構造に合わせて引数として渡すようにすれば、式全体を再構成して出力することが可能になる。以前の研究 \cite{FCA19} ではこの方法を用いて try-with 構文を含む言語のステッパを実装することに成功した。
%しかし、try-with のような制御構文を含む言語では、コンテキストを区切ってある区間のコンテキストを一度に捨てるなどの操作が必要になるため、コンテキストの構造が一次元的でなくなり、複雑なデータ構造の定義が必要になる可能性がある \cite{FCA19} 。

%ところが、図\ref{figure:lambda_stepper}の\texttt{c}型の定義を見ると、各コンストラクタはインタプリタ関数の「どの再帰呼び出しか」に対応している。コンテキストの型は評価順序によって定まるものであり、評価順序はインタプリタ関数で定義されているので、コンテキストを表すデータ型の定義はインタプリタ関数から導出できるものであると考えられる。次節以降ではその導出方法の1つを提案する。
